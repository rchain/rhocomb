RqP2R    . Req      ::= ":rho" RCPProc ;
RqY2R    . Req      ::= ":rho" RCYProc ;
RqP2Y    . Req      ::= ":cc" RCPProc ;

RqEval   . Req      ::= RCRProc ;
RqCmd    . Req      ::= ":q" ;

_        . RCPProc  ::= RCPProc1 ;
_        . RCPProc1 ::= "{" RCPProc "}" ;
RCPZero  . RCPComp  ::= "0" ;
RCPInp   . RCPComp  ::= "for" "(" RCUName "<-" RCUName ")" RCPProc1 ;
RCPOutp  . RCPComp  ::= RCUName "!" "(" RCUName ")" ;

RCPInj   . RCPProc1 ::= RCPComp ;
RCPRep   . RCPProc1 ::= "*" RCPProc1 ;
RCPNew   . RCPProc1 ::= "new" [RCUName] "in" RCPProc1 ;

RCPPar   . RCPProc  ::= RCPProc "|" RCPProc1 ;

_        . RCYProc  ::= RCYProc1 ;
_        . RCYProc1 ::= "{" RCYProc "}" ;
RCYZero  . RCYComb  ::= "z" ;
RCYMsg   . RCYComb  ::= "m" "(" RCUName "," RCUName ")" ;
RCYKill  . RCYComb  ::= "k" "(" RCUName ")" ;
RCYDup   . RCYComb  ::= "d" "(" RCUName "," RCUName "," RCUName ")" ;
RCYSeq   . RCYComb  ::= "s" "(" RCUName "," RCUName "," RCUName ")" ;
RCYFwd   . RCYComb  ::= "fw" "(" RCUName "," RCUName ")" ;
RCYBrl   . RCYComb  ::= "bl" "(" RCUName "," RCUName ")" ;
RCYBrr   . RCYComb  ::= "br" "(" RCUName "," RCUName ")" ;

RCYInj   . RCYProc1 ::= RCYComb ;
RCYStr   . RCYProc1 ::= "*" RCYProc1 ;
RCYNew   . RCYProc1 ::= "new" [RCUName] "in" RCYProc1 ;

RCYPar   . RCYProc  ::= RCYProc "|" RCYProc1 ;

-- Names
RCUNWild . RCUName  ::= "_" ;
RCUNVar  . RCUName  ::= Var ;
separator RCUName ";" ;

_        . RCRProc  ::= RCRProc1 ;
_        . RCRProc1 ::= "{" RCRProc "}" ;
RCRZero  . RCRComb  ::= "0" ;
RCRMsg   . RCRComb  ::= "m" "(" RCRName "," RCRName ")" ;
RCRKill  . RCRComb  ::= "k" "(" RCRName ")" ;
RCRDup   . RCRComb  ::= "d" "(" RCRName "," RCRName "," RCRName ")" ;
RCRSeq   . RCRComb  ::= "s" "(" RCRName "," RCRName "," RCRName ")" ;
RCRFwd   . RCRComb  ::= "fw" "(" RCRName "," RCRName ")" ;
RCRBrl   . RCRComb  ::= "bl" "(" RCRName "," RCRName ")" ;
RCRBrr   . RCRComb  ::= "br" "(" RCRName "," RCRName ")" ;

RCRInj   . RCRProc1 ::= RCRComb ;
RCRStr   . RCRProc1 ::= "*" "(" RCRName ")" ;
RCRPar   . RCRProc  ::= RCRProc "|" RCRProc1 ;

RCRQuot  . RCRName  ::= "@" RCRProc ;

token Var (((letter | '\'') (letter | digit | '_' | '\'')*)|(('_') (letter | digit | '_' | '\'')+)) ;
